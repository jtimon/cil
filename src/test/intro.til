mode script

import("src/core/modes/test")

assert_eq_str(loc(), loc(), "src/test/intro.til:5:22:") // TODO fix, the col should be one less

//// Intro to the language (suggestion: uncomment lines which claim will fail and see what happens)
// @ // fails because @ is invalid on the lexer for now
// adsgffdgfdg sdfgfsdgsf @ dfghj dgfhj // fails because @ is invalid on the lexer, but at a different line and col

// Declarations and assignments:

AAA : Bool = and(true, false); // Explicit types are allowed in declarations, but optional
BBB := or(false, and(true, true)) // It is a strongly typed language, but allows type inference (not in arguments yet, never in return types)
// AAA := true // fails, re-declaration of user constant 'AAA'
// AAA = false // fails, assignment to a constant, all declarations are constant by default
mut CCC := true // variables need the mut keyword, both as variables and as arguments
CCC = false; // assignments to mut declarations (aka variables/mut arguments) are allowed
// and := true // fails, re-declaration of core func 'and'
// Note that ';' between statements is allowed, but optional
// CCC = false;; // fails because ';;' is not allowed, I can't think of an example where that would make sense

// Functions types:

// Note that proc 'abc_proc' is used before being defined, but it is defined in the root context of the file
abc_proc(true, true, true)
// TODO explicitly implement closures or equivalent before fixing this bug
abc_proc(false, false, false)

// Procedures or procs can be declared and directly called in most modes including script
abc_proc := proc(a: Bool, b: Bool, c: Bool) {

    abc := proc(a: Bool, b: Bool, c: Bool) {
        println("inside abc, three Bools: ", a.to_str(), ", ", b.to_str(), ", ", c.to_str())
    }
    abc(a, b, c)
}

// Pure functions or funcs are similar to procs, but they can't have side effects, thus they cannot call to procedures
my_func := func(a: Bool, b: Bool) returns Bool {
    // println("proc") // fails, funcs cannot call procs, println is a core proc
    // abc_proc() // fails, funcs cannot call procs, abc_proc is a custom proc
    // return 42 // fails, because 42 is not a Bool
    return and(a, b)
}
test(loc(), my_func(true, true), "my_func(true, true) didn't return true")

// return can be used in functions that don't return anything to exit early
exit_early_print := proc(a: Bool, msg: Str) {
    println(msg)
    if a {
        return
    }
    println("exit_early_print didn't exit early")
}
exit_early_print(false, "Calling: exit_early_print(false, msg)")
exit_early_print(true, "Calling: exit_early_print(true, msg)")

// Functions can return multiple values
my_func_multi := func(a: Bool, b: Bool) returns Bool, Bool {
    return a, b
}
// _a, _b := my_func_multi(true, true) // TODO multiple declarations
// _a, _b = my_func_multi(true, true) // TODO multiple assignments

// TODO treat macros differently, currently they're equivalent to procs
my_macro := macro(a: Bool, b: Bool) returns Bool {
    return and(a, b)
}
test(loc(), my_macro(true, true), "my_macro(true, true) didn't return true")

my_str_func := func(a: Str) returns Str {
    return a
}
arg_test_str := "strings can be passed as arguments and returned"
assert_eq_str(loc(), my_str_func(arg_test_str), arg_test_str)

main := proc() {
    mut my_bool := true
    my_bool = my_func(my_bool, my_func(true, my_func(true, true)))
    res := my_func(true, true)
    my_bool = and(res, my_func(true, my_func(true, true)))
    my_bool = my_func(res, my_func(true, my_func(true, true)))

    // abc(false, true, false) // fails, because abc is defined locally in proc 'abc_proc'
}
// 'mode script' doesn't need to declare a main proc, but it can, and also call it
main()

// panic(loc(), "panic panics when uncommented") // fails, because it panics

// fails because mut func/procs shouldn't be allowed at this context level, in this mode, or perhaps never
// mut assertm2 := proc(cond: Bool, msg: Str) {}
